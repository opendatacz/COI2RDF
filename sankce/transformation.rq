PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX gr:      <http://purl.org/goodrelations/v1#>
PREFIX lex:     <http://purl.org/lex#>
PREFIX metalex: <http://www.metalex.eu/metalex/1.0#> 
PREFIX odcz:    <http://linked.opendata.cz/ontology/coi.cz/>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX schema:  <http://schema.org/>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX void:    <http://rdfs.org/ns/void#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?sanction a odcz:Sanction ; 
    skos:notation ?ID_Sankce ;
    dcterms:valid ?effectiveDate ;
	odcz:zakon ?ZÃ¡kon ;
    odcz:paragraf ?Paragraf ;
    void:inDataset ?dataset ;
    schema:result ?fine
    .

  ?inspection schema:result ?sanction .

  ?fine a odcz:Fine ;
    gr:hasCurrencyValue ?sanctionValue ;
    gr:hasCurrency "CZK"
    .

}
WHERE {
  # Base URI
  BIND ("http://linked.opendata.cz/resource/domain/" AS ?baseUri)
  
  # Static URIs
  BIND (URI("http://www.coi.cz/") AS ?coi)
  BIND (URI("http://linked.opendata.cz/resource/dataset/coi.cz/kontroly-sankce-zakazy") AS ?dataset)
  
  # Mint per-row URIs
  BIND (URI(CONCAT(?baseUri, "coi.cz/sanction/", ENCODE_FOR_URI(?ID_Sankce))) AS ?sanction)
  BIND (URI(CONCAT(?baseUri, "coi.cz/check-action/", ENCODE_FOR_URI(?ID_kontroly))) AS ?inspection)
  BIND (URI(CONCAT(?baseUri, "coi.cz/sanction/", ENCODE_FOR_URI(?ID_Sankce) ,"/price-specification")) AS ?fine)
   
  # Datatyped literals
  BIND (REPLACE(
    ?Datum_nabyti_Pravni_Moci,
    "^(\\d{1,2}).*$",
    "$1"
  ) AS ?effectiveDateMonth)
  BIND (REPLACE(
    ?Datum_nabyti_Pravni_Moci,
    "^(\\d{1,2})/(\\d{1,2}).*$",
    "$2"
  ) AS ?effectiveDateDay)
  BIND (REPLACE(
    ?Datum_nabyti_Pravni_Moci,
    "^(\\d{1,2})/(\\d{1,2})/(\\d{4})$",
    "$3"
  ) AS ?effectiveDateYear)
  BIND (STRDT(CONCAT(
    ?effectiveDateYear,
    "-",
    IF(STRLEN(?effectiveDateMonth) < 2, CONCAT("0", ?effectiveDateMonth), ?effectiveDateMonth),
    "-",
    IF(STRLEN(?effectiveDateDay) < 2, CONCAT("0", ?effectiveDateDay), ?effectiveDateDay)
  ), xsd:date) AS ?effectiveDate)
  BIND (STRDT(?Vyse_pokuty, xsd:decimal) AS ?sanctionValue)
}
OFFSET 1
