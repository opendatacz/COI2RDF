PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX gr:      <http://purl.org/goodrelations/v1#>
PREFIX lex:     <http://purl.org/lex#>
PREFIX metalex: <http://www.metalex.eu/metalex/1.0#> 
PREFIX odcz:    <http://linked.opendata.cz/ontology/coi.cz/>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX schema:  <http://schema.org/>
PREFIX void:    <http://rdfs.org/ns/void#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
  ?ban a odcz:Ban ;
    odcz:zakon ?Dle_zakona ;
    gr:includesObject ?typeAndQuantityNode ;
    gr:hasPriceSpecification ?priceSpecification ;
    void:inDataset ?dataset ;
    skos:notation ?ID_zakazu
    .

  ?inspection schema:result ?ban .
  
  ?typeAndQuantityNode a gr:TypeAndQuantityNode ;
    gr:typeOfGood ?someItems ;
    gr:hasUnitOfMeasurement ?unit ; 
    gr:amountOfThisGood ?amount
    .
  
  ?someItems a gr:SomeItems ;
    gr:category ?category
    .

  ?priceSpecification a odcz:Fine ;
    gr:hasCurrency "CZK" ;
    gr:hasCurrencyValue ?banValue
    .
}
WHERE {
  # Base URI
  BIND ("http://linked.opendata.cz/resource/domain/" AS ?baseUri)
  
  # Static URIs
  BIND (URI("http://www.coi.cz/") AS ?coi)
  BIND (URI("http://linked.opendata.cz/resource/dataset/coi.cz/kontroly-sankce-zakazy") AS ?dataset)
  
  # Mint per-row URIs
  BIND (URI(CONCAT(?baseUri, "coi.cz/ban/", ENCODE_FOR_URI(?ID_zakazu))) AS ?ban)
  BIND (URI(CONCAT(?baseUri, "coi.cz/check-action/", ENCODE_FOR_URI(?ID_kontroly))) AS ?inspection)
  BIND (URI(CONCAT(
    ?baseUri, "coi.cz/ban/", ENCODE_FOR_URI(?ID_zakazu),
    "/price-specification"
  )) AS ?priceSpecification)
  BIND (URI(CONCAT(?baseUri, "coi.cz/ban/", ENCODE_FOR_URI(?ID_zakazu),"/type-and-quantity-node")) AS ?typeAndQuantityNode)
  BIND (URI(CONCAT(?baseUri, "coi.cz/ban/", ENCODE_FOR_URI(?ID_zakazu), "/some-items")) AS ?someItems)

  BIND (URI(CONCAT(?baseUri, "coi.cz/ban/", ENCODE_FOR_URI(?ID_zakazu), "/act")) AS ?act) 
   
  # Datatyped literals
  BIND (STRDT(?Cena_celkem, xsd:decimal) AS ?banValue)
  BIND (STRDT(?Mnozstvi, xsd:decimal) AS ?amount)
  
  # Translation to UN/CEFACT unit codes
  VALUES (?Jednotka   ?unit) {
         ("kilogram"  "KGM")
         ("kus"       "C62")
         ("litr"      "LTR")
  }
  
  # Language-tagged literals
  BIND (STRLANG(?Sortiment, "cs") AS ?category)
}
OFFSET 1
